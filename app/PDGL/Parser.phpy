// vim: syntax=yacc

%pure_parser
%expect 6

%token T_FINAL
%token T_CLASS
%token T_EXTENDS
%token T_IMPLEMENTS
%token T_DIRECT
%token T_VAL
%token T_VAR

%token T_TRIPLE_BACKQUOTE
%token T_COLON_ASSIGN
%token T_ARROW_ASSIGN

%token T_LITERAL
%token T_NUMBER

%token T_STRING_SQ
%token T_STRING_DQ

%right T_GAP

%start file

%%

// <General rules>

literal
    : T_LITERAL
    | T_DIRECT
    | T_VAL
    | T_VAR
    ;

identifier
    : literal   { $$ = new PHPNode\Identifier($1); }
    ;

identifier_ex_name
    : literal
    | T_FINAL
    | T_CLASS
    | T_EXTENDS
    | T_IMPLEMENTS
    ;

identifier_ex
    : identifier_ex_name    { $$ = new PHPNode\Identifier($1); }
    ;

name_parts
    : literal                   { $$ = [$1]; }
    | name_parts '\\' literal   { $$ = array_merge($1, [$3]); }

name
    : name_parts
        {
            // Hack for emulate T_NAMESPACE token

            if (strtolower($1[0]) === 'namespace') {
                array_unshift($1);
                $$ = new PHPNode\Name\Relative($1);
            } else {
                $$ = new PHPNode\Name($1);
            }
        }
    | '\\' name_parts   { $$ = new PHPNode\Name\FullyQualified($2); }
    ;

names_list
    : name                  { $$ = [$1]; }
    | names_list ',' name   { $$ = array_merge($1, [$3]); }
    ;

type
    : name      { $$ = new Type($1, false); }
    | name '?'  { $$ = new Type($1, true); }
    ;

gap
    :       { $$ = ''; }
    | T_GAP
    ;

stmt_end
    : gap
    | ';'
    ;

comments
    :
        {
            // TODO
            // $$ = $this->lexer->getComments();

            $$ = $this->getComments();
        }
    ;

annotations
    :                   { $$ = []; }
    | annotations_req
    ;

annotations_req
    : annotation                    { $$ = [$1]; }
    | annotations_req annotation    { $$ = array_merge($1, [$2]); }
    ;

annotation
    : '@' annotation_name annotation_args
        {
            $$ = new Node\Annotation([
                'name'      => $2,
                'arguments' => $3
            ]);
        }
    ;

annotation_name
    : name
//  | T_ANNOTATION_NAME { $$ = new PHPNode\Name($1); }
    ;

annotation_args
    :                               { $$ = []; }
    | '(' annotation_args_list ')'  { $$ = $2; }
    ;

annotation_args_list
    :                           { $$ = []; }
    | annotation_args_list_req
    ;

annotation_args_list_req
    : annotation_arg                                { $$ = [$1]; }
    | annotation_args_list_req ',' annotation_arg   { $$ = array_merge($1, [$3]); }
    ;

annotation_arg
    : T_LITERAL { $$ = new PHPNode\Scalar\String_($1); }
//  | php_expr  { $$ = static::parsePHP($1); }
    ;

// </General rules>

file
    : file_stmts
    ;

file_stmts
    :                   { $$ = []; }
    | file_stmts_req
    ;

file_stmts_req
    : file_stmt                 { $$ = [$1]; }
    | file_stmts_req file_stmt  { $$ = array_merge($1, [$2]); }

file_stmt
    : class
    ;

class
    : comments annotations class_mods T_CLASS identifier class_extends class_implements '{' comments class_stmts '}' comments
        {
            $$ = new Node\Class_([
                'name' => $5,

                'flags' => $3,

                'extends'    => $6,
                'implements' => $7,

                'fields' => $10,

                'attributes' => [
                    'comments'    => $1,
                    'annotations' => $2
                ]

            ]);
        }
    ;

class_mods
    :                   { $$ = 0; }
    | class_mods_req
    ;

class_mods_req
    : class_mod
    | class_mods_req class_mod  { $$ = static::applyFlag($1, $2); }
    ;

class_mod
    : T_FINAL       { $$ = Node\Class_::FLAG_FINAL; }
    | T_FINAL '!'   { $$ = Node\Class_::FLAG_FINAL_FINAL; }
//  | T_DATA        { $$ = Node\Class_::FLAG_DATA; }
    ;

class_extends
    :                   { $$ = null; }
    | T_EXTENDS name    { $$ = $2; }
    ;

class_implements
    :                           { $$ = []; }
    | T_IMPLEMENTS names_list   { $$ = $2; }
    ;

class_stmts
    :                   { $$ = []; }
    | class_stmts_req
    ;

class_stmts_req
    : class_stmt                    { $$ = [$1]; }
    | class_stmts_req class_stmt    { $$ = array_merge($1, [$2]); }
    ;

class_stmt
    : field
    ;

field
    : comments annotations field_mods field_keyword identifier_ex field_type field_defval stmt_end comments
        {
            $$ = new Node\Field([
                'name' => $5,

                'flags' => $3 | $4 | $7['assign'],

                'type' => $6,

                'default' => is_null($7['value'])?
                    null: static::parsePHP($7['value']),

                'attributes' => [
                    'comments'    => $1,
                    'annotations' => $2
                ]
            ]);

            $this->lexer->sendGaps = false;
        }
    ;

field_mods
    :           { $$ = 0; }
    | T_DIRECT  { $$ = Node\Field::FLAG_DIRECT; }
    ;

field_keyword
    : T_VAL { $$ = 0; }
    | T_VAR { $$ = Node\Field::FLAG_EDITABLE; }
    ;

field_type
    :           { $$ = new Type('mixed', false); }
    | ':' type  { $$ = $2; }
    ;

field_defval
    :                       { $$ = ['assign' => 0, 'value' => null]; }
    | assign gap php_expr   { $$ = ['assign' => $1, 'value'  => $3]; }
    ;

assign
    : '='               { $this->lexer->sendGaps = true; $$ = Node\Field::FLAG_FILTER_DEFAULT; }
    | T_COLON_ASSIGN    { $this->lexer->sendGaps = true; $$ = 0; }
    | T_ARROW_ASSIGN    { $this->lexer->sendGaps = true; $$ = Node\Field::FLAG_DIRECT_DEFINING; }
    ;

php_expr
    : php_expr_part
    | php_expr php_expr_part    { $$ = $1.$2; }
    ;

php_expr_part
    : identifier_ex_name | T_NUMBER
    | T_STRING_SQ | php_string_dq
    | php_op | T_GAP
    | '(' php_exprs ')' { $$ = $1.$2.$3; }
    | '[' php_exprs ']' { $$ = $1.$2.$3; }
    | '{' php_stmts '}' { $$ = $1.$2.$3; }
    ;

php_exprs
    :           { $$ = ''; }
    | php_expr
    ;

php_stmts
    :               { $$ = ''; }
    | php_stmts_req
    ;

php_stmts_req
    : php_stmt
    | php_stmts_req php_stmt    { $$ = $1.$2; }
    ;

php_stmt
    : php_expr
    | ';'
    ;

php_op
    : '$' | '\\' | '~' | '@' | '!' | '*' | '/' | '%' | '+' | '-' | '.' | ',' | '>' | '<' | '&' | '^' | '|' | '='

    | '*' '*'       { $$ = "**"; }
    | '+' '+'       { $$ = "++"; }
    | '-' '-'       { $$ = "--"; }
    | '<' '<'       { $$ = "<<"; }
    | '>' '>'       { $$ = ">>"; }
    | '<' '='       { $$ = "<="; }
    | '>' '='       { $$ = ">="; }
    | '=' '='       { $$ = "=="; }
    | '!' '='       { $$ = "!="; }
    | '<' '>'       { $$ = "<>"; }
    | '&' '&'       { $$ = "&&"; }
    | '|' '|'       { $$ = "||"; }
    | '?' '?'       { $$ = "??"; }
    | '?' ':'       { $$ = "?:"; }
    | '+' '='       { $$ = "+="; }
    | '-' '='       { $$ = "-="; }
    | '*' '='       { $$ = "*="; }
    | '/' '='       { $$ = "/="; }
    | '.' '='       { $$ = ".="; }
    | '%' '='       { $$ = "%="; }
    | '&' '='       { $$ = "&="; }
    | '|' '='       { $$ = "|="; }
    | '^' '='       { $$ = "^="; }
    | '=' '>'       { $$ = "=>"; }
    | '-' '>'       { $$ = "->"; }
    | '=' '=' '='   { $$ = "==="; }
    | '!' '=' '='   { $$ = "!=="; }
    | '<' '=' '>'   { $$ = "<=>"; }
    | '*' '*' '='   { $$ = "**="; }
    | '<' '<' '='   { $$ = "<<="; }
    | '>' '>' '='   { $$ = ">>="; }
    ;

php_string_dq
    : T_STRING_DQ
    | php_string_dq T_STRING_DQ { $$ = $1.$2; }
    ;

%%

    protected function getComments(): array {
        $ret = [];

        $gaps = $this->lexer->getGaps();
        foreach ($gaps as $gap) {
            if ((int) preg_match_all('#//.*#', $gap->value, $matches) > 0) {
                foreach ($matches[0] as $comment) {
                    $ret[] = $comment;
                }
            }
        }

        return $ret;
    }

    public static function applyFlag(int $flags, int $flag): int {
        if ($flags & $flag) {
            $this->yyerror('Flag has already set');
        }

        return $flags | $flag;
    }

    public static function parsePHP(string $value): PHPNode\Expr {
        static $parser = null;

        if (is_null($parser)) {
            $parser = (new ParserFactory())->create(ParserFactory::ONLY_PHP7);
        }

        return $parser->parse("<?php $value;")[0]->expr;
    }
